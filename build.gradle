// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    apply from: "config.gradle"

    final localProp = new Properties()
    final localPropFile = new File(rootDir, "local.properties")
    localProp.load(new FileInputStream(localPropFile))
    println("local.properties:\n$localProp ")

    final projectProp = new Properties()
    final projectPropFile = new File(rootDir, "project.properties")
    boolean publishFastCommon = false
    if (projectPropFile.exists()) {
        projectProp.load(new FileInputStream(projectPropFile))
        println("project.properties:\n$projectProp ")
        if (projectProp.containsKey('publishFastCommon')) {
            publishFastCommon = projectProp['publishFastCommon'] == "true"
            if (publishFastCommon) {
                ext.bintrayUser = localProp['bintrayUser']
                ext.bintrayKey = localProp['bintrayKey']
            }
        }
    }

    ext.publishFastCommon = publishFastCommon
    ext.sampleUseRemoteLib = projectProp['sampleUseRemoteLib'] == "true"

    println("publishFastCommon: $publishFastCommon")
    println("sampleUseRemoteLib: $sampleUseRemoteLib")

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        if (publishFastCommon) {
            classpath 'com.novoda:bintray-release:0.9.2'
        }

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
